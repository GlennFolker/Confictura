pluginManagement{
    repositories{
        gradlePluginPortal()
        maven{url 'https://jitpack.io'}
    }
}

if(JavaVersion.current().ordinal() < JavaVersion.VERSION_17.ordinal()){
    throw new GradleException("JDK 17 is a required minimum version. Yours: ${System.getProperty('java.version')}")
}

rootProject.name = modArtifact
for(def local : ['glTFrenzy', 'EntityAnno']){
    def prop = "local.${local.toLowerCase()}"
    if(!hasProperty(prop) || Boolean.valueOf(getProperty(prop) as String)){
        def dir = new File(rootDir.parent, local)
        if(dir.exists()){
            logger.log(LogLevel.LIFECYCLE, "Compiling with local `$local`.")
            includeBuild dir
        }
    }
}
